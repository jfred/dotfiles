# set to 1 for profiling
local -i profile_me=0
if [ -f ~/.zsh_debug ]; then
    profile_me=1
fi

if [[ 1 -eq ${profile_me} ]]; then
    zmodload zsh/zprof
fi

# Override in ~/.localrc to disable modules
# The following example would disable java and vagrant
#     DOT_EXCLUDE='(java|vagrant)' 
DOT_EXCLUDE='_no_matches_'

# load config and system specific
[[ -f ~/.localrc ]] && source ~/.localrc
setopt extendedglob

export PATH=${PATH}:~/bin:${DOTFILES}/bin

# load all zsh and bin for enabled topics
for config_file (${DOTFILES}/**/*.zsh) do
    if [[ ! ${config_file} =~ ${DOT_EXCLUDE} ]]; then
        source ${config_file}
    fi
done
for bin (${DOTFILES}/**/bin); do
    if [[ ! ${bin} =~ ${DOT_EXCLUDE} ]]; then
        export PATH=${PATH}:${bin}
    fi
done

# checking cached .zcompdump only once a day
autoload -Uz compinit
if [[ -n ${ZDOTDIR}/.zcompdump(#qN.mh+24) ]]; then
	compinit;
else
	compinit -C;
fi;

# matches case insensitive for lowercase
zstyle ':completion:*' matcher-list 'm:{a-z}={A-Z}'

[ -n ${DOTFILES_POST_INIT+1} ] && eval ${DOTFILES_POST_INIT}

# for profiling
if [[ 1 -eq ${profile_me} ]]; then
    zprof
fi
