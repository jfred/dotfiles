# for profiling
[[ -v ZSH_PROFILE ]] && zmodload zsh/zprof

zmodload zsh/datetime

[[ -v ZSH_TIME_STARTUP ]] && t0=$EPOCHREALTIME

# Override in ~/.localrc to disable modules
# The following example would disable java and vagrant
#     DOT_EXCLUDE='(java|vagrant)' 
DOT_EXCLUDE='_no_matches_'

# load config and system specific
[[ -f ~/.localrc ]] && source ~/.localrc
setopt extendedglob

export PATH=${PATH}:~/bin:${DOTFILES}/bin

# threshold where config is configured slow
WARN_THRESHOLD=1.0
function source_warn {
    local -i t0
    t0=$EPOCHREALTIME
    source ${1}
    runtime=$[EPOCHREALTIME-t0]
    [[ $runtime -gt $WARN_THRESHOLD ]] && echo "WARN sourcing $1 took ${runtime}s"
}

# load all zsh and bin for enabled topics
for config_file (${DOTFILES}/**/*.zsh) do
    if [[ ! ${config_file} =~ ${DOT_EXCLUDE} ]]; then
        source_warn ${config_file}
    fi
done
for bin (${DOTFILES}/**/bin); do
    if [[ ! ${bin} =~ ${DOT_EXCLUDE} ]]; then
        export PATH=${PATH}:${bin}
    fi
done

# checking cached .zcompdump only once a day
autoload -Uz compinit
if [[ -n ${ZDOTDIR}/.zcompdump(#qN.mh+24) ]]; then
	compinit;
else
	compinit -C;
fi;

# matches case insensitive for lowercase
zstyle ':completion:*' matcher-list 'm:{a-z}={A-Z}'

[ -n ${DOTFILES_POST_INIT+1} ] && eval ${DOTFILES_POST_INIT}

# for profiling
[[ -v ZSH_PROFILE ]] && zprof
[[ -v ZSH_TIME_STARTUP ]] && echo $[EPOCHREALTIME-t0]
